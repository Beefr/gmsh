
// Post-processing primitives in parsed format

General.FastRedraw = 0 ;
General.Color.Background = White ;
General.Color.Foreground = Black ;
General.Color.Text = Black ;

View "default"{
  SP(0,10,0){1} ;                                                  // a scalar point
  VP(0,8,0){1,0,0} ;                                               // a vector point
  SL(0,6,0, 1,6,0){1,2} ;                                          // a scalar line
  VL(0,4,0, 1,4,0){1,0,0, 2,0,0} ;                                 // a vector line
  ST(0,2,0, 1,2,0, 0,3,0){1,2,3} ;                                 // a scalar triangle
  VT(0,0,0, 1,0,0, 0,1,0){1,0,0, 2,0,0, 3,0,0} ;                   // a vector triangle
  SS(0,-2,0, 1,-2,0, 0,-1,0, 0,-2,1){1,2,3,4} ;                    // a scalar tetrahedron
  VS(0,-4,0, 1,-4,0, 0,-3,0, 0,-4,1){1,0,0, 2,0,0, 3,0,0, 4,0,0} ; // a vector tetrahedron
  SQ(0,-6,0, 1,-6,0, 1,-5,0, 0,-5,0){1,2,3,4} ;                    // a scalar quadrangle
  VQ(0,-8,0, 1,-8,0, 1,-7,0, 0,-7,0){1,0,0, 2,0,0, 3,0,0, 4,0,0} ; // a vector quadrangle
  SH(0,-10,0, 1,-10,0, 1,-9,0, 0,-9,0,
     0,-10,1, 1,-10,1, 1,-9,1, 0,-9,1){1,2,3,4,4,3,2,1} ;          // a scalar hexahedron
  VH(0,-12,0, 1,-12,0, 1,-11,0, 0,-11,0, 
     0,-12,1, 1,-12,1, 1,-11,1, 0,-11,1)
    {1,0,0, 2,0,0, 3,0,0, 4,0,0, 4,0,0, 3,0,0, 2,0,0, 1,0,0};      // a vector hexahedron
  SI(0,-14,0, 1,-14,0, 0,-13,0,
     0,-14,1, 1,-14,1, 0,-13,1){1,2,3,3,2,1} ;                     // a scalar prism
  VI(0,-16,0, 1,-16,0, 0,-15,0,
     0,-16,1, 1,-16,1, 0,-15,1)
    {1,0,0, 2,0,0, 3,0,0, 3,0,0, 2,0,0, 1,0,0} ;                   // a vector prism
};

Duplicata View[0];
View[1].Name = "with-options" ;
View[1].OffsetX = 3 ;
View[1].IntervalsType = 2 ;
View[1].ShowElement = 1 ;
View[1].ArrowLocation = 2 ;
View[1].ColorTable = {Red,Green,Magenta,Cyan,Brown,Pink} ;

Duplicata View[0];
View[2].Name = "with-options-2" ;
View[2].OffsetX = 6 ;
View[2].IntervalsType = 4 ;
View[2].ShowElement = 1 ;
View[2].Format = "%.1f" ;
